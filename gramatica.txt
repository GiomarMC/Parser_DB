Programa -> ListaSentencias.

ListaSentencias -> Comando ListaSentencias
                | ε.

Comando -> START_SENTENCIA CreateDatabase END_SENTENCIA newline
         | START_SENTENCIA CreateTable END_SENTENCIA newline
         | START_SENTENCIA Select END_SENTENCIA newline
         | START_SENTENCIA Insert END_SENTENCIA newline.

CreateDatabase -> CREATE DATABASE identificador.

CreateTable -> CREATE TABLE identificador ( ListaColumnasDef ).

ListaColumnasDef -> identificador TipoColumna ListaColumnasDefPrime.

ListaColumnasDefPrime -> coma identificador TipoColumna ListaColumnasDefPrime
                       | ε.

TipoColumna -> INT
             | VARCHAR
             | BOOLEAN.

Select -> SELECT Proyeccion FROM identificador CondicionResultado.

Proyeccion -> '*' 
           | ListaColumnas.

ListaColumnas -> identificador ListaColumnasPrime.

ListaColumnasPrime -> coma identificador ListaColumnasPrime
                    | ε.

CondicionResultado -> Condicion CondicionResultadoPrime
                    | Resultado.

CondicionResultadoPrime -> Resultado 
                         | ε.

Condicion -> WHERE ExpresionCondicional.

Resultado -> separador identificador.

ExpresionCondicional -> Termino OperadorRelacional Termino ExpresionCondicionalPrime.

ExpresionCondicionalPrime -> OperadorLogico ExpresionCondicional
                           | ε.

OperadorRelacional -> 'igual' 
                    | 'diferente' 
                    | 'menor' 
                    | 'menorigual' 
                    | 'mayor' 
                    | 'mayorigual'.

OperadorLogico -> and 
                | or.

Termino -> identificador 
         | numero
         | cadena.

Insert -> INSERT INTO identificador InsertData.

InsertData -> ( ListaColumnas ) VALUES ( ListaValores ).

ListaValores -> Valor ListaValoresPrime.

ListaValoresPrime -> coma Valor ListaValoresPrime
                   | ε.

Valor -> numero
       | cadena
       | NULL.